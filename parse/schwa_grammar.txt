MAIN = W PROGRAM W
PROGRAM! = STATEMENT W ( ";" W PROGRAM | EMPTY )
BLOCK = PROGRAM

STATEMENT! = DO | IF | SWITCH | DECLARATION | FUNCTION | REWARD | ASSIGNMENT | EMPTY

DECLARATION = "var" W JUDGEMENT
JUDGEMENT = IDENTIFIER W ":" W TYPENAME 
REPETITIVE = "do" W "{" W DOLOOP
REWARD = "$" W ARITH
ASSIGNMENT = IDENTIFIER W "=" W ARITH

DO = "do" W DOBODY 
DOBODY! = "{" W DOBODYCONTENTS W "}" 
DOBODYCONTENTS! = LOGIC W "~>" W BLOCK W ( DOBODYCONTENTS | EMPTY )

IF = "if" W IFBODY 
IFBODY! = "{" W IFBODYCONTENTS W "}" 
IFBODYCONTENTS! = LOGIC W "~>" W BLOCK W ( IFBODYCONTENTS | EMPTY )

SWITCH = "switch" W SWITCHARGS W SWITCHBODY 
SWITCHARGS = TUPLE 
SWITCHBODY! = "{" W SWITCHBODYCONTENTS W "}" 
SWITCHBODYCONTENTS! = "~>" W BLOCK W ( SWITCHBODYCONTENTS | EMPTY )

TUPLE = "(" W TUPLECONTENTS W ")" 
TUPLECONTENTS! = ( ARITH W ( "," W TUPLECONTENTS | EMPTY ) ) | EMPTY

TYPENAME = "bool" | "float" | "int"
FUNCTION = "def" W FUNCTIONHEADER W FUNCTIONBODY
FUNCTIONHEADER = IDENTIFIER W FUNCTIONTYPE
FUNCTIONTYPE = "(" W FUNCTIONARGLIST W ")" W ":" W TYPENAME
FUNCTIONBODY! = "{" W BLOCK W "}"
FUNCTIONARGLIST! = ( JUDGEMENT W ( "," W FUNCTIONARGLIST | EMPTY ) ) | EMPTY

LOGIC = LOGICEXPR 
LOGICEXPR! = LOGICTERM W ( "||" W LOGICEXPR | EMPTY )
LOGICTERM! = LOGICFACTOR W ( "&&" W LOGICTERM | EMPTY )
LOGICFACTOR! = "!" W LOGICFACTOR | "(" W LOGICEXPR W ")" | ARITHEXPR W ( "==" | "!=" | "<=" | "<" | ">=" | ">" ) W ARITHEXPR

ARITH = ARITHEXPR
ARITHEXPR! = ARITHTERM W ( ( "+" | "-" ) W ARITHEXPR | EMPTY )
ARITHTERM! = "-" W ARITHTERM | ARITHFACTOR W ( ( "*" | "/" ) W ARITHTERM | EMPTY )
ARITHFACTOR! = "(" W ARITHEXPR W ")" | IDENTIFIERLOOP | NUMBER 

NUMBER = NUMBERLOOP ( "." NUMBERLOOP | EMPTY )
NUMBERLOOP! = DIGIT ( NUMBERLOOP | EMPTY )
DIGIT* = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

IDENTIFIER = IDENTIFIERLOOP
IDENTIFIERLOOP! = ALPHA ( IDENTIFIERLOOP | EMPTY )
ALPHA* = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "_" 

W* = WHITE
WHITE* = " " WHITE | EMPTY
EMPTY* = ""
