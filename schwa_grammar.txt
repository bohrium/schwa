MAIN = W PROGRAM W
PROGRAM! = STATEMENT W ( ";" W PROGRAM | EMPTY )

STATEMENT = REPETITIVE | CONDITIONAL | "skip" | "abort" | DECLARATION | FUNCTION | REWARD | ASSIGNMENT | EMPTY

DECLARATION = "var" W JUDGEMENT
JUDGEMENT = IDENTIFIER W ":" W TYPENAME 
REPETITIVE = "do" W "{" W DOLOOP
CONDITIONAL = "if" W "{" W IFLOOP
REWARD = "$" W ARITHEXPR 
ASSIGNMENT = IDENTIFIER W "=" W ARITHEXPR

IFLOOP! = GUARD W PROGRAM W ( "}" | IFLOOP )
DOLOOP! = GUARD W PROGRAM W ( "}" | DOLOOP )
GUARD = LOGICEXPR W "~>" 

TYPENAME = "bool" | "float" | "int"
FUNCTION = "def" W IDENTIFIER W "(" W ARGLIST W ":" W TYPENAME W "{" W PROGRAM W "}" 
ARGLIST = JUDGEMENT W ( ARGLIST | EMPTY ) | W ")" 

LOGICEXPR! = LOGICTERM W ( "or" W LOGICEXPR | EMPTY )
LOGICTERM! = LOGICFACTOR W ( "and" W LOGICTERM | EMPTY )
LOGICFACTOR = "not" W LOGICFACTOR | "(" W LOGICEXPR W ")" | ARITHEXPR W ( "==" | "!=" | "<=" | "<" | ">=" | ">" ) W ARITHEXPR

ARITHEXPR! = ARITHTERM W ( ( "+" | "-" ) W ARITHEXPR | EMPTY )
ARITHTERM! = "-" W ARITHTERM | ARITHFACTOR W ( ( "*" | "/" ) W ARITHTERM | EMPTY )
ARITHFACTOR = "(" W ARITHEXPR W ")" | IDENTIFIER | NUMBER 

NUMBER = NUMBERLOOP ( "." NUMBERLOOP | EMPTY )
NUMBERLOOP! = DIGIT ( NUMBERLOOP | EMPTY )
DIGIT* = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

IDENTIFIER = IDENTIFIERLOOP
IDENTIFIERLOOP! = ALPHA ( IDENTIFIERLOOP | EMPTY )
ALPHA* = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "_" 

W* = WHITE
WHITE* = " " WHITE | EMPTY
EMPTY* = ""
